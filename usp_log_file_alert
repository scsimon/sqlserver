/*************************************************************************************************
Procedure to send an email with log file size, open transactions against that database,
and query information from sp_WhoIsActive. Was designed to be executed when an Agent Alert
triggered for tempdb log file size > ___ MB. Often, the result is a hung or long running transaction
against tempdb. This transaction can prevent the log from releasing space. 

Note, data file growth is seperate from this procedure's purpose entirely, and a transaction
which is consuming the most pages in tempdb isn't necessairly the culprit that is preventing
the log from releasing space.

2018-05-15		S.Simon		Created Proc
*************************************************************************************************/



--create procedure usp_log_file_alert( @database varchar(256) = 'tempdb'
--									 ,@mailProfile varchar(256) = null									
--									 ,@operator varchar(256) = null										
--									 ,@email varchar(4000) = null							
--									 ,@emailResults bit = 0
--									 )
--as


declare @database varchar(256) = 'tempdb'
declare @mailProfile varchar(256) = null									--mail profile used for sp_send_dbmail. We get a profile if left null, with default profile as precedence 
declare @operator varchar(256) = null										--operator profile to send the email to
declare @email varchar(4000) = 'email@domain.com'							--email address / addresses to send email to. @operator email used if both passed in
declare @emailResults bit = 0

-----------------------------------------------------------------------------
--variables for email, and other places in proc
--yes, it used tempdb which is likely what you are troubleshooting
--but hopefully you have a little room and your alerts don't wait until it's too late
-----------------------------------------------------------------------------

declare @body xml
declare @body_html varchar(max)
declare @errMsg varchar(4000)
declare @subject varchar(1000)
declare @SPID varchar(16)
declare @datetime varchar(64)
declare @badguy varchar(max) = ''
declare @badquery varchar(max) = ''

---------------------------------------------------------------------------------
--get @operator email if the param was used, otherwise use @email
---------------------------------------------------------------------------------


if(@operator is null and @email is null and @emailResults = 1)
begin
	raiserror('Email or Operator is required when @emailResults = 1',16,1)
	return
end
else
begin
	declare @operator_email varchar(256) = coalesce(@email,(select email_address from msdb..sysoperators where upper([name]) = upper(@operator)))

	if (@operator_email is null and @emailResults = 1)
		begin
			set @errMsg = 'No email address set for operator' + @operator
			raiserror(@errMsg,16,1)
			return
		end
end


---------------------------------------------------------------------------------
--get the default @mailProfile for db_mail if one isn't passed in
---------------------------------------------------------------------------------

if @mailProfile is null and @emailResults = 1
begin

	--exec msdb.dbo.sysmail_help_profile_sp 
	--exec msdb.dbo.sysmail_help_account_sp 

	if object_id('tempdb..#profile') is not null 
	drop table #profile
	create table #profile (  principal_id int
							,principal_name varchar(256)
							,profile_id int
							,profile_name varchar(256)
							,is_default bit)

	insert into #profile
	exec msdb.dbo.sysmail_help_principalprofile_sp  

	set @mailProfile = (select top 1 profile_name from #profile where is_default = 1)
end


--suppose they didn't set a default? Get an arbritary one...

if @mailProfile is null and @emailResults = 1
begin
	set @mailProfile = (select top 1 profile_name from #profile)
end

--still nada? throw an error
if (@mailProfile is null and @emailResults = 1)
	begin
		set @errMsg = 'No mail profiles set up--emails can not be sent'
		raiserror(@errMsg,16,1)
		return
	end


	
-----------------------------------------------------------------------------
--get the longest running transaction... likely the culprit
-----------------------------------------------------------------------------
if object_id('tempdb..#opentran') is not null 
drop table #opentran

create table #opentran (ActiveTransaction  varchar(256), 
						Details sql_variant)   


insert into #opentran
exec('dbcc opentran(' + @database + ') with tableresults')


set @SPID = (select cast(Details as varchar(16)) from #opentran where ActiveTransaction = 'OLDACT_SPID')  
set @datetime = (select cast(Details as varchar(64)) from #opentran where ActiveTransaction = 'OLDACT_STARTTIME') 

-----------------------------------------------------------------------------
--pre 2012 use dbcc sqlperf
-----------------------------------------------------------------------------

if(select left(convert(varchar(16),serverproperty('ProductVersion')),charindex('.',convert(varchar(16),serverproperty('ProductVersion')),0) - 1)) < 11
begin

	if object_id('tempdb..#logspace') is not null 
	drop table #logspace

	create table #logspace (DatabaseName varchar(64), 
							LogSizeMB decimal(32,2), 
							LogSpaceUsedPercentage decimal(5,2),
							status bit)
	insert into #logspace
	exec('dbcc sqlperf(''logspace'')')

	set @body = 
	(select AdminTools.dbo.createHTMLTable (
	(select DatabaseName, LogSizeMB, LogSpaceUsedPercentage, [status] from #logspace for xml path ('row'),elements xsinil)
	,null,null,null))

end

-----------------------------------------------------------------------------
--2012 onward, use dmv
-----------------------------------------------------------------------------

if(select left(convert(varchar(16),serverproperty('ProductVersion')),charindex('.',convert(varchar(16),serverproperty('ProductVersion')),0) - 1)) > 11
begin
	
	if object_id('tempdb..#logspaceusage') is not null 
	drop table #logspaceusage

	create table #logspaceusage (DatabaseName varchar(64), 
								LogSizeMB decimal(32,2), 
								UsedLogSizeMB decimal(32,2),
								LogSpaceUsedPercentage decimal(5,2))

	declare @sql varchar(max) = 
	'use [' + @database + ']
	select 
		database_name = '''+ @database + '''
		,log_size_mb = cast(total_log_size_in_bytes / 1048576.0 as decimal(32,2))
		,used_log_size_mb = cast(used_log_space_in_bytes / 1048576.0 as decimal(32,2))
		,used_log_space_in_percent = cast(used_log_space_in_percent as decimal(5,2))
	from sys.dm_db_log_space_usage' 

	insert into #logspaceusage
	exec(@sql)

	set @body = 
	(select AdminTools.dbo.createHTMLTable (
	(select DatabaseName, LogSizeMB, UsedLogSizeMB, LogSpaceUsedPercentage from #logspaceusage for xml path ('row'),elements xsinil)
	,null,null,null))

end

-----------------------------------------------------------------------------
--Get some sp_WhoIsActive
-----------------------------------------------------------------------------

if @SPID is not null
begin
	if object_id('tempdb..#whoIsActive') is not null
	drop table #whoIsActive
			
	create table #whoIsActive ( run_time varchar(15)
								,sql_text xml
								,login_name sysname
								,tran_status varchar(30)
								,[program_name] sysname
								,[host_name] sysname)
			
	exec AdminTools.dbo.sp_whoIsActive  @destination_table = #whoIsActive, @filter_type = 'session', @filter = @SPID, @show_own_spid = 0, @output_column_list = '[dd%][sql_text][login_name][status][program_name][host_name]'
			
	--select * from #whoIsActive

	set @badguy =	(select cast(login_name as varchar(256)) from #whoIsActive) 
					+ ' executed the query below via '
					+ (select cast(isnull([program_name],'') as varchar(4000)) from #whoIsActive)
					+ ' from '
					+ (select cast(isnull([host_name],'') as varchar(1000)) from #whoIsActive)
					+ '. The transaction is currently '
					+ (select cast(isnull(tran_status,'') as varchar(64)) from #whoIsActive)
					+ ' and has been running for '
					+ (select cast(run_time as varchar(16)) from #whoIsActive)
					+ '.'

	set @badquery = (select replace(replace(cast(isnull(sql_text,'') as varchar(max)),'<',''),'>','') from #whoIsActive)
			
	--todo... find out how to send formatted query in the html build
	--set @badquery = (select cast(sql_text as varchar(max)) from #whoIsActive)
			
end


-----------------------------------------------------------------------------
--Send email, maybe
-----------------------------------------------------------------------------

if (@emailResults = 1)
begin	
	--add some CSS, and a message
	set @body_html =  
		'<style type="text/css" media="screen,print">
		.center
		{
			text-align: center;
		}
		table,th
		{
			border: 1px solid black;
		}
		table,tr
		{
			border: 1px solid black;
		}
		table,td
		{
			border: 1px solid black;
		}
		</style>
		<H3>The longest active transaction on ' + @database 
					+ ' is SPID '	
					+ @SPID 
					+ ' which has been running since ' 
					+ @datetime 
					+ '.</H3><br></br>'
					+ cast(@body as varchar(max))
					+ '<br></br>'
					+ @badguy
					+ '<br></br>'
					+ @badquery
					+ '<br></br>'

	--catch all just in case
	if (@body_html is null)
	begin
		set @body_html = '<H3>The alert procedure encounted an error... manually troubleshoot the log file growth with dbcc opentran(''' + @database + '''), sp_WhoIsActive, and dbcc sqlperf(''logspace'')</H3>'
	end


		
	--send the email
	set @subject = 'Log File Size Alert for ' + @database + ' on ' + @@SERVERNAME

	exec msdb..sp_send_dbmail
	@profile_name = @mailProfile	
	,@recipients = @operator_email
	,@subject = @subject
	,@body = @body_html
	,@body_format = 'HTML'
end

else

-----------------------------------------------------------------------------
--They don't want the email...
-----------------------------------------------------------------------------

begin
	if object_id('tempdb..#logspace') is not null 
		select * from #logspace where DatabaseName = @database
	if object_id('tempdb..#logspaceusage') is not null 
		select * from #logspaceusage
	if object_id('tempdb..#opentran') is not null 
		select * from #opentran
	if object_id('tempdb..#whoIsActive') is not null 
		select * from #whoIsActive
end
